plugins {
    id "org.sonarqube" version "2.3"
}
allprojects {
    group = 'neo4j-drivers'
    version = '0.0.4-SNAPSHOT'
}

ext {
    guavaVersion = '21.0'
    junitVersion = '4.12'
    neo4jDriverVersion = '1.3.0-alpha01'
    neo4jVersion = '3.1.1'
}

// Theres properties can be separated to a gradle.properties file
// It should contain the elements like:
//    systemProp.sonar.host.url=http://localhost:9000
//    systemProp.sonar.login=token
sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.login", "admin"  // Default sonar username
        property 'sonar.password', 'admin'  // Default sonar password
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'  // Used for generating coverage report
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    jar { from sourceSets.main.allJava }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
    }

    configurations.all { exclude group: 'asm' }
    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
            html.destination "${project.buildDir}/reports/jacoco/html"
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)  // Every "gradle test" will be followed by a "gradle jacocoTestReport"
    jacocoTestReport.dependsOn(test)  // Since jacoco uses test results we want "gradle jacocoTestReport" to run a "gradle test" first automatically

    publishing {
        publications {
            mavenJava(MavenPublication) { from components.java }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: 'file:///tmp/mvn_repo')
            }
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: junitVersion
    }
}
